options {
  JAVA_UNICODE_ESCAPE = true;
}

PARSER_BEGIN(MiniJavaParser)
package myparser;

  import syntaxtree.*;
  public class MiniJavaParser {}
PARSER_END(MiniJavaParser)

SKIP : /* WHITE SPACE */
{
  " "
| "\t"
| "\n"
| "\r"
| "\f"
}

SPECIAL_TOKEN : /* COMMENTS */
{
  <SINGLE_LINE_COMMENT: "//" (~["\n","\r"])* ("\n"|"\r"|"\r\n")>
| <FORMAL_COMMENT: "/**" (~["*"])* "*" ("*" | (~["*","/"] (~["*"])* "*"))* "/">
| <MULTI_LINE_COMMENT: "/*" (~["*"])* "*" ("*" | (~["*","/"] (~["*"])* "*"))* "/">
}

TOKEN :
{
  < LPAREN: "(" >
| < RPAREN: ")" >
| < LSQPAREN: "[" >
| < RSQPAREN: "]" >
| < LBRACE: "{" >
| < RBRACE: "}" >
| < SEMICOLON: ";" >
| < COMMA: "," >
| < DOT: "." >
| < ASSIGN: "=" >
| < LT: "<" >
| < PLUS: "+" >
| < MINUS: "-" >
| < TIME: "*" >
| < AND : "&&" >
| < NOT : "!" >
| < BOOLEAN: "boolean" >
| < CLASS: "class" >
| < ELSE: "else" >
| < EXTENDS: "extends" >
| < FALSE: "false" >
| < IF: "if" >
| < WHILE: "while" >
| < INTEGER: "int" >
| < DOUBLE: "double" >
| < LENGTH: "length" >
| < MAIN: "main" >
| < NEW: "new" >
| < PUBLIC: "public" >
| < RETURN: "return" >
| < STATIC: "static" >
| < STRING: "String" >
| < THIS: "this" >
| < TRUE: "true" >
| < PRINT: "System.out.println" >
| < VOID: "void" >
}

TOKEN : /* LITERALS */
{
  < INTEGER_LITERAL: ( ["1"-"9"] (["0"-"9"])* | "0" ) >
}

TOKEN : /* IDENTIFIERS */
{
  < IDENTIFIER: <LETTER> (<LETTER>|<DIGIT>)* >
|
  < #LETTER:
      [
       "\u0024",
       "\u0041"-"\u005a",
       "\u005f",
       "\u0061"-"\u007a",
       "\u00c0"-"\u00d6",
       "\u00d8"-"\u00f6",
       "\u00f8"-"\u00ff",
       "\u0100"-"\u1fff",
       "\u3040"-"\u318f",
       "\u3300"-"\u337f",
       "\u3400"-"\u3d2d",
       "\u4e00"-"\u9fff",
       "\uf900"-"\ufaff"
      ]
  >
|
  < #DIGIT:
      [
       "\u0030"-"\u0039",
       "\u0660"-"\u0669",
       "\u06f0"-"\u06f9",
       "\u0966"-"\u096f",
       "\u09e6"-"\u09ef",
       "\u0a66"-"\u0a6f",
       "\u0ae6"-"\u0aef",
       "\u0b66"-"\u0b6f",
       "\u0be7"-"\u0bef",
       "\u0c66"-"\u0c6f",
       "\u0ce6"-"\u0cef",
       "\u0d66"-"\u0d6f",
       "\u0e50"-"\u0e59",
       "\u0ed0"-"\u0ed9",
       "\u1040"-"\u1049"
      ]
  >
}


/************************************
 * The MiniJava Grammar Starts Here *
 ************************************/


Program Goal() :
{ MainClass m;
  ClassDeclList cl = new ClassDeclList();
  ClassDecl c;
}
{
  m=MainClass()
  ( c=ClassDeclaration() { cl.addElement(c); } )*
  <EOF>
  { return new Program(m,cl); }
}

MainClass MainClass() :
{ 
  Token tok1, tok2, tok3, tok4;
  IdentifierType i1;
  Identifier i2;
  Statement s; }
{
  tok1=<CLASS> i1=IdentifierType() "{"
    tok2=<PUBLIC> "static" tok4=<VOID> "main" "(" tok3=<STRING> "[" "]" i2=Identifier() ")"
    "{" s=Statement() "}"
  "}"
  { return new MainClass(i1,i2,s,tok1.beginLine,tok1.beginColumn,tok2.beginLine,tok2.beginColumn,tok3.beginLine,tok3.beginColumn,tok4.beginLine,tok4.beginColumn); }
}

ClassDecl ClassDeclaration() :
{ ClassDecl c; }
{
  (
  LOOKAHEAD(3)
  c=ClassDeclarationSimple()
|
  c=ClassDeclarationExtends()
  )
  { return c; }
}

ClassDecl ClassDeclarationSimple() :
{ Token tok;
  IdentifierType i;
  VarDecl v;
  VarDeclList vl = new VarDeclList();
  MethodDecl m;
  MethodDeclList ml = new MethodDeclList();
}
{
  tok=<CLASS> i=IdentifierType() "{" ( v=VarDeclaration()    { vl.addElement(v); } )*
                             ( m=MethodDeclaration() { ml.addElement(m); } )*
                       "}"
  { return new ClassDeclSimple(i,vl,ml,tok.beginLine,tok.beginColumn); }
}

ClassDecl ClassDeclarationExtends() :
{ Token tok;
  IdentifierType i1,i2;
  VarDecl v;
  VarDeclList vl = new VarDeclList();
  MethodDecl m;
  MethodDeclList ml = new MethodDeclList();
}
{
  tok=<CLASS> i1=IdentifierType() "extends" i2=IdentifierType()
                       "{"   ( v=VarDeclaration()    { vl.addElement(v); } )*
                             ( m=MethodDeclaration() { ml.addElement(m); } )*
                       "}"
  { return new ClassDeclExtends(i1,i2,vl,ml,tok.beginLine,tok.beginColumn); }
}

VarDecl VarDeclaration() :
{ Type t;
  Identifier i;
}
{
  t=Type() i=Identifier() ";"
  { return new VarDecl(t,i); }
}

MethodDecl MethodDeclaration() :
{
  Token tok;
  Type t;
  Identifier i;
  FormalList fl = new FormalList();
  VarDecl v;
  VarDeclList vl = new VarDeclList();
  Statement s;
  StatementList sl = new StatementList();
  Exp e;
}
{
  tok=<PUBLIC> t=Type() i=Identifier()
  "(" ( fl=FormalParameterList() )? ")"
  "{" ( LOOKAHEAD(2) v=VarDeclaration() { vl.addElement(v); } )*
      ( s=Statement() { sl.addElement(s); } )* "return" e=Expression() ";" "}"
  { return new MethodDecl(t,i,fl,vl,sl,e,tok.beginLine,tok.beginColumn); }
}

FormalList FormalParameterList() :
{ FormalList fl = new FormalList(); 
  Formal f;
}
{
  f=FormalParameter() { fl.addElement(f); }
  ( f=FormalParameterRest() { fl.addElement(f); } )*
  { return fl; }
}

Formal FormalParameter() :
{ Type t; 
  Identifier i;
}
{
  t=Type() i=Identifier()
  { return new Formal(t,i); }
}

Formal FormalParameterRest() :
{ Formal f; }
{
  "," f=FormalParameter()
  { return f; }
}

Type Type() :
{ Type t; }
{
  (
  LOOKAHEAD(3)
  t=ArrayType()
|
  t=BooleanType()
|
  t=IntegerType()
|
  t=DoubleType()
|
  t=IdentifierType()
  )
  { return t; }
}

Type ArrayType() :
{Token t;}
{
  t=<INTEGER> "[" "]"
  { return new IntArrayType(t.beginLine, t.beginColumn); }
}

Type BooleanType() :
{Token t;}
{
  t=<BOOLEAN>
  { return new BooleanType(t.beginLine, t.beginColumn); }
}

Type IntegerType() :
{Token t;}
{
  t=<INTEGER>
  { return new IntegerType(t.beginLine, t.beginColumn); }
}

Type DoubleType() :
{Token t;}
{
  t=<DOUBLE>
  { return new DoubleType(t.beginLine, t.beginColumn); }
}

IdentifierType IdentifierType() :
{ Token t; }
{
  (
  t=<IDENTIFIER>
  { return new IdentifierType(t.image, t.beginLine, t.beginColumn); }
|
  t=<MAIN>
  { return new IdentifierType("main", t.beginLine, t.beginColumn); }
  )
}

Statement Statement() :
{ Statement s; }
{
  (
  s=Block()
|
  LOOKAHEAD(2)
  s=AssignmentStatement()
|
  LOOKAHEAD(2)
  s=ArrayAssignmentStatement()
|
  s=IfStatement()
|
  s=WhileStatement()
|
  s=PrintStatement()
  )
  { return s; }
}

Statement Block() :
{
  Statement s;
  StatementList sl = new StatementList();
}
{
  "{" ( s=Statement() { sl.addElement(s); } )* "}"
  { return new Block(sl); }
}

Statement AssignmentStatement() :
{ Identifier i;
  Exp e;
}
{
  i=Identifier() "=" e=Expression() ";"
  { return new Assign(i,e); }
}

Statement ArrayAssignmentStatement() :
{ Identifier i;
  Exp e1,e2;
}
{
  i=Identifier() "[" e1=Expression() "]" "=" e2=Expression() ";"
  { return new ArrayAssign(i,e1,e2); }
}

Statement IfStatement() :
{ Exp e;
  Statement s1,s2;
}
{
  "if" "(" e=Expression() ")" s1=Statement() "else" s2=Statement()
  { return new If(e,s1,s2); }
}

Statement WhileStatement() :
{ Exp e;
  Statement s;
}
{
  "while" "(" e=Expression() ")" s=Statement()
  { return new While(e,s); }
}

Statement PrintStatement() :
{ Exp e; }
{
  "System.out.println" "(" e=Expression() ")" ";"
  { return new Print(e); }
}

Exp Expression() :
{ Exp e1, e2; }
{
  e1=ExpressionF()
  (
  "&&" e2=ExpressionF()
  { e1 = new And(e1,e2); }
  )*
  { return e1; }
}

Exp ExpressionF() :
{ Exp e1, e2; }
{
  e1=ExpressionE()
  (
  "<" e2=ExpressionE()
  { e1 = new LessThan(e1,e2); }
  )*
  { return e1; }
}


Exp ExpressionE() :
{ Exp e1, e2; }
{
  e1=ExpressionD()
  (
  "+" e2=ExpressionD()
  { e1 = new Plus(e1,e2); }
|
  "-" e2=ExpressionD()
  { e1 = new Minus(e1,e2); }
  )*
  { return e1; }
}

Exp ExpressionD() :
{ Exp e1, e2; }
{
  e1=ExpressionB()
  (
  "*" e2=ExpressionB()
  { e1 = new Times(e1,e2); }
  )*
  { return e1; }
}

Exp ExpressionB() :
{ Token t;
  Exp e; }
{
  (
  t=<NOT> e=ExpressionB()
  { e = new Not(e,t.beginLine,t.beginColumn); }
|
  e=ExpressionA()
  )
  { return e; }
}


Exp ExpressionA() :
{ Exp e1, e2;
  Identifier i;
  ExpList e3; }
{
  e1=PrimaryExpression()
  (
  "[" e2=Expression() "]"
  { e1 = new ArrayLookup(e1,e2); }
|
  LOOKAHEAD(3)
  "." i=Identifier() "("
  { e3 = new ExpList(); }
  (
  e2=Expression() { e3.addElement(e2); }
  ( "," e2=Expression() { e3.addElement(e2); } )*
  )? ")"
  { e1 = new Call(e1,i,e3); }
|
  "." "length"
  { e1 = new ArrayLength(e1); }
  )*
  { return e1; }
}

Exp PrimaryExpression() :
{ Exp e;
  Token t;
  IdentifierType i;
}
{ 
  (
  t=<INTEGER_LITERAL>
  { e = new IntegerLiteral(Integer.decode(t.image),t.beginLine,t.beginColumn); }
|
  t=<TRUE>
  { e = new True(t.beginLine,t.beginColumn); }
|
  t=<FALSE>
  { e = new False(t.beginLine,t.beginColumn); }
|
  t=<IDENTIFIER>
  { e = new IdentifierExp(t.image, t.beginLine, t.beginColumn); }
|
  t=<MAIN>
  { e = new IdentifierExp("main", t.beginLine, t.beginColumn); }
|
  t=<THIS>
  { e = new This(t.beginLine,t.beginColumn); }
|
  LOOKAHEAD(3)
  t=<NEW> "int" "[" e=Expression() "]"
  { e = new NewArray(e,t.beginLine,t.beginColumn); }
|
  t=<NEW> i=IdentifierType() "(" ")"
  { e = new NewObject(i,t.beginLine,t.beginColumn); }
|   
  t=<LPAREN> e=Expression() ")"
  )
  { e.line=t.beginLine; e.column=t.beginColumn; return e; }
}

Identifier Identifier() :
{ Token t; }
{
  (
  t=<IDENTIFIER>
  { return new Identifier(t.image, t.beginLine, t.beginColumn); }
|
  t=<MAIN>
  { return new Identifier("main", t.beginLine, t.beginColumn); }
  )
}

